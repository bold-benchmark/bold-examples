PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX ssn: <http://www.w3.org/ns/ssn/>
PREFIX ua: <http://example.org/opcua#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dct: <http://purl.org/dc/terms/>

INSERT {
    GRAPH ?p {
        ?p a sosa:ObservableProperty, ssn:Property, ua:Variable .
    }
} WHERE {
    ?system ssn:hasProperty ?p .
};

DELETE {
    ?bnode rdfs:label ?label ;
           dct:identifier ?id .
} INSERT {
    GRAPH ?var {
        ?var a ua:Variable ;
             rdfs:label ?label .
    }
} WHERE {
    ?bnode a ssn:Property ;
           rdfs:label ?label ;
           dct:identifier ?id .

    # TODO add sameAs links between UA variables and DA variables.

    FILTER (isBlank(?bnode) && strstarts(?id, "%"))

    BIND (iri(strafter(?id, "%")) AS ?var)
};

INSERT {
    ?p rdfs:seeAlso ?var .

    GRAPH ?var {
        ?var rdf:value "null"^^rdf:JSON .
    }
} WHERE {
    ?p a ssn:Property .

    BIND (uri(concat(str(?p), "/Variable")) AS ?var)
}